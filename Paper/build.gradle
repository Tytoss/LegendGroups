import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id 'com.gradleup.shadow' version "9.0.0-beta13"
}

group = 'de.tytoss'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven { url = uri("https://jitpack.io") }
    maven { url = uri("https://repo.codemc.io/repository/maven-releases/") }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")

    implementation project(':Core')

    implementation("io.projectreactor:reactor-core:3.5.13")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-reactor:1.7.3")
    implementation 'org.spongepowered:configurate-yaml:4.1.2'
    implementation('com.github.retrooper:packetevents-spigot:2.11.0')
}

tasks {
  runServer {
    minecraftVersion("1.21")
  }
}

def targetJavaVersion = 24
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(ShadowJar).configureEach {
    archiveClassifier.set("")
    mergeServiceFiles()

    from(rootProject.project(":Core").getExtensions().getByType(SourceSetContainer.class).getByName("main").getOutput())

    setConfigurations (
            Collections.singletonList(
                    project.getConfigurations().getByName("runtimeClasspath")
            )
    )

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
